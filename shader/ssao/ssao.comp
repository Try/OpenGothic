#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_control_flow_attributes : enable

#include "scene.glsl"
#include "common.glsl"

layout(local_size_x = 8, local_size_y = 8) in;

layout(push_constant, std140) uniform PushConstant {
  mat4 mvp;
  mat4 mvpInv;
  };

layout(binding  = 0) uniform writeonly image2D result;

layout(binding  = 1) uniform sampler2D diffuse;
layout(binding  = 2) uniform sampler2D normals;
layout(binding  = 3) uniform sampler2D depthBuf;

layout(binding  = 4, std140) uniform UboScene {
  SceneDesc scene;
  };

const uint  numSamples = 32;
const uint  numIter    = 8;
const float sphereLen  = 100;

ivec2 fragCoord;
ivec2 screenSize;
mat3  tangent;

shared uint vMin, vMax, vDiff;
shared uint shMaxSteps;

bool isSolved(float occlusion, float dx) {
  vMin  = 256;
  vMax  = 0;
  vDiff = 0;
  barrier();

  if(occlusion>=0) {
    uint v = uint(occlusion*255+0.5);
    uint d = uint(dx       *255+0.5);

    atomicMin(vMin,  v);
    atomicMax(vMax,  v);
    atomicMax(vDiff, d);
    }
  memoryBarrierShared();
  barrier();

  uint diff = vMax - vMin;

  return (diff<4) || (vDiff<4);
  }

uint hash(uint x) {
  x += ( x << 10u );
  x ^= ( x >>  6u );
  x += ( x <<  3u );
  x ^= ( x >> 11u );
  x += ( x << 15u );
  return x;
  }

uint hash(uvec2 v) {
  return hash(v.x ^ hash(v.y));
  }

float floatConstruct(uint m) {
  const uint ieeeMantissa = 0x007FFFFFu; // binary32 mantissa bitmask
  const uint ieeeOne      = 0x3F800000u; // 1.0 in IEEE binary32

  m &= ieeeMantissa;                     // Keep only mantissa bits (fractional part)
  m |= ieeeOne;                          // Add fractional part to 1.0

  float  f = uintBitsToFloat( m );       // Range [1:2]
  return f - 1.0;                        // Range [0:1]
  }

vec3 inverse(vec3 pos) {
  vec4 ret = mvpInv*vec4(pos,1.0);
  return (ret.xyz/ret.w);
  }

vec3 project(vec3 pos) {
  vec4 ret = mvp*vec4(pos,1.0);
  return (ret.xyz/ret.w);
  }

float readZ(in vec2 posf) {
  return textureLod(depthBuf, posf.xy*0.5+vec2(0.5), 0).r;
  }

float sampleRadius(uint i, uint maxSmp) {
  return 0.5+float(i)/float(2*maxSmp);
  }

vec2 calcOcclussion(const vec3 at0, const vec3 pos0, const float f0, uint seed, uint step) {
  float occlusion = 0, weightAll = 0;

  const uint maxStep = numSamples/numIter;

  [[loop]]
  for(uint i0=0; i0<maxStep; ++i0) {
    uint  i      = i0*step + seed;
    float r      = sampleRadius(i,numSamples);
    vec3  h      = sampleHemisphereCos(i,numSamples,f0).xzy;

    vec3  v      = tangent * h;
    vec3  at1    = project(pos0 + v*sphereLen*r);
    if(abs(at1.x)>1.0 || abs(at1.y)>1.0) {
      continue;
      }

    float z      = readZ(at1.xy);
    bool  isHit  = floor(z*32768.0) < floor(at1.z*32768.0);
    if(!isHit) {
      float lenQ   = r*r;
      float distW  = 1.0-min(lenQ/(sphereLen*sphereLen),1); // distance attenuation
      weightAll += distW;
      continue;
      }

    vec3  at2    = vec3(at1.xy,z);
    vec4  pos2   = mvpInv*vec4(at2,1.0);
    pos2.xyz     = (pos2.xyz/pos2.w);

    vec3  dp     = (pos2.xyz-pos0);
    float lenQ   = dot(dp,dp);
    float distW  = 1.0-min(lenQ/(sphereLen*sphereLen),1); // distance attenuation

    occlusion += distW;
    weightAll += distW;
    }

  return vec2(occlusion,weightAll);
  }

int estimateMaxSteps(float z) {
  if(z>=0.999)
    return 0; // sky

  // NOTE: manual tuning been here
  const float depth = packHiZ(z); // linearDepth(z, scene.clipInfo);
  if(depth < 0.05)
    return 4;
  if(depth < 0.6)
    return 5;
  if(depth <= 0.8)
    return 8;
  if(depth <= 0.9)
    return 7;
  if(depth <= 0.95)
    return 4;
  if(depth <= 0.99)
     return 2;
  return 1;
  }

void main() {
  shMaxSteps = 0;
  barrier();

  screenSize = ivec2(textureSize(depthBuf,0));
  fragCoord  = ivec2(gl_GlobalInvocationID.xy);

  const bool valid = all(lessThan(fragCoord, screenSize));

  if(valid) {
    vec3 norm = normalize(texelFetch(normals,fragCoord,0).xyz*2.0-vec3(1.0));
    // Compute a tangent frame and rotate the half vector to world space
    vec3 up   = abs(norm.z) < 0.999f ? vec3(0.0f, 0.0f, 1.0f) : vec3(1.0f, 0.0f, 0.0f);
    tangent[0] = normalize(cross(up, norm));
    tangent[1] = cross(norm, tangent[0]);
    tangent[2] = norm;
    }

  float occlusion = -1, prev= -1;
  vec2  occ       = vec2(0,0.0001);

  const vec2  inPos = vec2(2*fragCoord+ivec2(1,1))/vec2(screenSize)-vec2(1,1);
  const vec3  at0   = vec3(inPos.xy,readZ(inPos.xy));
  const vec3  pos0  = inverse(at0);
  const uint  h0    = hash(fragCoord);
  const float f0    = M_PI*2.0*floatConstruct(h0);

  int maxSteps = estimateMaxSteps(at0.z);
  atomicMax(shMaxSteps, maxSteps);
  barrier();
  // maxSteps = int(shMaxSteps);

  int  dbg  = 0;
  bool eval = valid;
  for(int i=0; i<shMaxSteps; ++i) {
    if(eval && i<maxSteps) {
      dbg       = i+1;
      prev      = occlusion;
      occ      += calcOcclussion(at0, pos0, f0, i, numIter);
      occlusion = (occ.x/occ.y);
      }

    if(i==0)
      continue;
    if(isSolved(occlusion, abs(occlusion-prev))) {
      // NOTE: 'break' here cause device-lost on Intel O_o
      eval = false;
      }
    }

  if(valid)
    imageStore(result, ivec2(gl_GlobalInvocationID.xy), vec4(2.0*occlusion));
  // imageStore(result, ivec2(gl_GlobalInvocationID.xy), vec4(float(dbg+1)/255.0));
  }
