#version 450

#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_samplerless_texture_functions : enable
#extension GL_EXT_control_flow_attributes : enable

#include "rtsm/rtsm_common.glsl"
#include "scene.glsl"
#include "common.glsl"

layout(local_size_x = 512) in;

const uint NumThreads = gl_WorkGroupSize.x*gl_WorkGroupSize.y*gl_WorkGroupSize.z;

layout(binding = 0, std140) uniform  UboScene    { SceneDesc   scene;                    };
layout(binding = 1, std430) readonly buffer Lbo  { LightSource lights[];                 };
layout(binding = 2, std430)          buffer VId  { uvec4       count;   LightId data[];  } vLights;
layout(binding = 3, r32ui)  uniform  uimage2D    lightBins;
layout(binding = 4, std430) readonly buffer Cbo  { Cluster     clusters[];               };
layout(binding = 5, std430)          buffer Pos  { uint        alloc;  uint  data[];     } pos;

#include "rtsm/rtsm_scratch.glsl"

shared uint meshletsId[2048];
shared uint numMeshlets;

shared uint meshListPtr;

void storeBin(uint lightId, uint ptr, uint num) {
  if(gl_LocalInvocationIndex!=0)
    return;
  vLights.data[lightId].ptr = ptr;
  imageStore(lightBins, ivec2(lightId,0), ivec4(num,0,0,0));
  }

bool isMeshletVisible(uint ptr, const LightSource lx) {
  const vec4  sphere = pullMeshSphere(ptr);
  const vec3  dv     = lx.pos - sphere.xyz;
  const float r      = lx.range + sphere.w;
  return dot(dv, dv) <= r*r;
  }

void traceMeshlets(uint listPtr, uint visCount, const LightSource lx) {
  const uint laneID = gl_LocalInvocationIndex;

  numMeshlets = 0;
  barrier();

  for(uint i=laneID; i<visCount; i+=NumThreads) {
    const uint ptr = pos.data[listPtr + i];
    if(!isMeshletVisible(ptr, lx))
      continue;

    const uint id = atomicAdd(numMeshlets, 1);
    if(id<meshletsId.length())
      meshletsId[id] = ptr;
    }
  }

void store(uint lightId) {
  const uint laneID = gl_LocalInvocationIndex;
  if(laneID==0)
    meshListPtr = allocScratch(numMeshlets);
  barrier();

  if(meshListPtr==NULL) {
    storeBin(lightId, NULL, 0);
    return;
    }

  storeBin(lightId, meshListPtr, numMeshlets);
  for(uint i=laneID; i<numMeshlets; i+=NumThreads) {
    pos.data[meshListPtr + i] = meshletsId[i];
    }
  }

void main() {
  const LightSource lx = lights[vLights.data[gl_WorkGroupID.x].id];

  traceMeshlets(1, pos.data[0], lx);
  barrier();

  if(numMeshlets < meshletsId.length()) {
    store(gl_WorkGroupID.x);
    return;
    }
  //TODO: 2-pass
  storeBin(gl_WorkGroupID.x, NULL, 0);
  }
