#version 450

#extension GL_GOOGLE_include_directive    : enable
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_samplerless_texture_functions : enable
// bindless
#extension GL_EXT_nonuniform_qualifier : enable
// #extension GL_KHR_memory_scope_semantics : enable

#include "common.glsl"
#include "scene.glsl"
#include "rtsm/rtsm_common.glsl"

#define TRANSLUCENCY 1

layout(local_size_x = 8, local_size_y = 8) in;

const uint NumThreads = gl_WorkGroupSize.x*gl_WorkGroupSize.y*gl_WorkGroupSize.z;

layout(binding = 0, r8)     uniform  image2D    outputImage;
layout(binding = 1, std140) uniform  UboScene   { SceneDesc scene; };
layout(binding = 2)         uniform  utexture2D gbufNormal;
layout(binding = 3)         uniform  texture2D  depth;
layout(binding = 4)         uniform  utexture2D primitiveBins;
layout(binding = 5, std430) readonly buffer Pos { uint alloc; uint data[]; } pos;

layout(binding = 7)         uniform  texture2D  textureMain[];
layout(binding = 8)         uniform  sampler    samplerMain;

#include "rtsm/rtsm_scratch.glsl"

ivec2 fragCoord = ivec2(gl_GlobalInvocationID.xy);

float alphaTest(const uint bucketId, vec3 bc, float areaInv, vec2 a, vec2 b, vec2 c) {
  const float u = dot(vec3(a.x,b.x,c.x), bc)*areaInv;
  const float v = dot(vec3(a.y,b.y,c.y), bc)*areaInv;

  //TODO: find optimal lod
  return textureLod(sampler2D(textureMain[nonuniformEXT(bucketId)], samplerMain), vec2(u,v), 0).a;
  }

float alphaTest(const uint bucketId, vec3 bc, float areaInv, uint a, uint b, uint c) {
  return alphaTest(bucketId, bc, areaInv, unpackHalf2x16(a), unpackHalf2x16(b), unpackHalf2x16(c));
  }

// utility
shared uvec4 rayBbox;
shared uint  rayZmin;
void rayBboxses(const vec3 ray, bool activeRay) {
  rayBbox = uvec4(0xFFFFFFFF, 0xFFFFFFFF, 0, 0);
  rayZmin = 0xFFFFFFFF;
  barrier();

  if(activeRay) {
    atomicMin(rayBbox.x, floatToOrderedUint(ray.x));
    atomicMin(rayBbox.y, floatToOrderedUint(ray.y));
    atomicMax(rayBbox.z, floatToOrderedUint(ray.x));
    atomicMax(rayBbox.w, floatToOrderedUint(ray.y));
    atomicMin(rayZmin,   floatToOrderedUint(ray.z));
    }
  }

vec4 worldPos(ivec2 frag, float depth) {
  const vec2 fragCoord = ((frag.xy+0.5)*scene.screenResInv)*2.0 - vec2(1.0);
  const vec4 scr       = vec4(fragCoord.x, fragCoord.y, depth, 1.0);
  return scene.viewProjectLwcInv * scr;
  }

// pagging
int shadowLod(vec2 dx, vec2 dy) {
  float px     = dot(dx, dx);
  float py     = dot(dy, dy);
  float maxLod = 0.5 * log2(max(px, py)); // log2(sqrt()) = 0.5*log2()
  float minLod = 0.5 * log2(min(px, py));

  const float bias = 0;
  return max(0, int(minLod + bias));
  }

int calcMipIndex(in vec2 shPos) {
  float d  = max(abs(shPos.x), abs(shPos.y));
  uint  id = uint(d);
  return findMSB(id)+1;
  }

// ray managment
vec3 calcRayOrigin(const vec4 wpos) {
  vec4 shPos = scene.viewVirtualShadowLwc * wpos;
  shPos.xyz /= shPos.w;
  return shPos.xyz;
  }

// meshlet-data
shared uint   primitiveBucket [NumThreads];
shared float  primitiveAreaInv[NumThreads];
shared Vertex meshVertices    [NumThreads*3];
bool isPrimitiveVisible(const vec4 aabb) {
  //float depthMax;

  const vec4 rbbox = orderedUintToFloat(rayBbox);
  // if(uintBitsToFloat(rayZmin) > depthMax)
  //   continue;
  if(!bboxIntersect(aabb, rbbox))
    return false;
  return true;
  }

uvec2 pullPrimitiveList() {
  ivec2 tileCoord = (fragCoord/RTSM_BIN_SIZE);
  return texelFetch(primitiveBins, ivec2(tileCoord), 0).xy;
  }

void pullPrimitives(const uint pkg) {
  const uint  laneID  = gl_LocalInvocationIndex;
  const uint  maxMesh = MeshletHeaderSize;

  const uint  ptr = (pkg >> 8);
  const uint  pId = pkg & 0xFF;

  const uint  primCnt  = pullPrimitiveCount(ptr);
  const uint  bucketId = pullMeshBucketId(ptr);
  const uvec4 tri      = pullPrimitiveFull(ptr+maxMesh, pId);
  const uint  ptrV     = ptr + maxMesh + primCnt;

  const Vertex a    = pullVertexFull(ptrV, tri.x);
  const Vertex b    = pullVertexFull(ptrV, tri.y);
  const Vertex c    = pullVertexFull(ptrV, tri.z);

  const vec4  tbb = bboxOf(a.pos.xy, b.pos.xy, c.pos.xy);
  if(!isPrimitiveVisible(tbb)) {
    primitiveAreaInv[laneID] = -1;
    return;
    }

  meshVertices[laneID*3+0] = a;
  meshVertices[laneID*3+1] = b;
  meshVertices[laneID*3+2] = c;
  primitiveAreaInv[laneID] = 1.0/edgeFunction(a.pos.xy, b.pos.xy, c.pos.xy);
  primitiveBucket[laneID]  = packUint2x16(uvec2(bucketId,tri.w));
  }

shared uint numActiveRays;
float tracePacket(const vec3 ray, float opacity, bool actRay) {
  const uint laneID  = gl_LocalInvocationIndex;

  if(actRay)
    atomicAdd(numActiveRays, 1);
  barrier();

  const uvec2 tileHeader    = pullPrimitiveList();
  const uint  numPrimitives = tileHeader.y & 0xFFFF;
  const uint  steps         = (numPrimitives + NumThreads - 1)/NumThreads;

  if((tileHeader.y & (1u<<31))!=0) {
    // error - emit 4x4 grid to display
    ivec2 tile = fragCoord/4;
    return (tile.x+tile.y)%2==0 ? 1.0 : 0.0;
    }

  bool  shadow  = !actRay;
  for(uint s=0; s<steps; ++s) {
    if(numActiveRays==0)
      break;

    const uint i = s*NumThreads + laneID;
    if(i<numPrimitives)
      pullPrimitives(pos.data[tileHeader.x + i]);
    barrier();

    const uint batchSize = min(numPrimitives-s*steps, NumThreads);
    for(uint id=0; id<batchSize; ++id) {
      if(shadow)
        break;

      const float areaInv = primitiveAreaInv[id];
      if(areaInv<0)
        continue;

      vec3  a  = meshVertices[id*3+0].pos;
      vec3  b  = meshVertices[id*3+1].pos;
      vec3  c  = meshVertices[id*3+2].pos;

      float w0 = edgeFunction(b.xy, c.xy, ray.xy);
      float w1 = edgeFunction(c.xy, a.xy, ray.xy);
      float w2 = edgeFunction(a.xy, b.xy, ray.xy);

      if(w0 < 0 || w1 < 0 || w2 < 0)
        continue;

      const vec3  bc    = vec3(w0,w1,w2);
      const float depth = dot(vec3(a.z,b.z,c.z), bc)*areaInv;
      if(depth <= ray.z)
        continue;

#if TRANSLUCENCY
      const uint flags = primitiveBucket[id] >> 16;
      if((flags & BK_SOLID)==0) {
        const uint  bucketId = primitiveBucket[id] & 0xFFFF;
        const float alpha    = alphaTest(bucketId, bc, areaInv, meshVertices[id*3+0].uv, meshVertices[id*3+1].uv, meshVertices[id*3+2].uv);
        opacity *= (1.0-alpha);
        } else {
        opacity = 0;
        }
#else
      opacity = 0;
#endif
      const float minOpacity = 0.49/255.0;
      if(opacity>minOpacity)
        continue;

      shadow = true;
      atomicAdd(numActiveRays, -1);
      break;
      }
    barrier();
    }

  return opacity;
  }

void main() {
  const ivec2 size   = textureSize(depth, 0);
  const bool  valid  = all(lessThan(fragCoord, size));

  const float z      = valid ? texelFetch(depth, fragCoord, 0).x  : 1.0;
  const vec3  normal = valid ? normalFetch(gbufNormal, fragCoord) : vec3(0);
  const float lamb   = imageLoad(outputImage, fragCoord).x;

  const vec4  wpos   = worldPos(fragCoord.xy, z) + vec4(normal*0.0015, 0);
  const vec3  ray    = calcRayOrigin(wpos);
  const bool  actRay = (lamb>0);

  numActiveRays = 0;
  rayBboxses(ray, actRay);
  barrier();

  const float opacity = tracePacket(ray, lamb, actRay);
  if(actRay && opacity<1)
    imageStore(outputImage, fragCoord, vec4(opacity));
  }
