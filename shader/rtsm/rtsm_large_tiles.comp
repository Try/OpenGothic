#version 450

#extension GL_GOOGLE_include_directive : enable

#include "rtsm/rtsm_common.glsl"
#include "scene.glsl"
#include "common.glsl"

layout(local_size_x = 16, local_size_y = 16) in;
const uint NumThreads = gl_WorkGroupSize.x*gl_WorkGroupSize.y*gl_WorkGroupSize.z;

layout(binding = 0)         uniform texture2D   outputImage;
layout(binding = 1, std140) uniform UboScene    { SceneDesc scene; };
layout(binding = 2)         uniform utexture2D  gbufNormal;
layout(binding = 3)         uniform texture2D   depth;
layout(binding = 5, std430)         buffer Pos  { uint alloc; uint  data[]; } pos;
layout(binding = 6, rg32ui) uniform writeonly uimage2D largeTiles;

#include "rtsm/rtsm_scratch.glsl"

shared uvec4 rayTileBbox[MaxSlices];
shared uint  rayTileMinDepth[MaxSlices];
shared uint  raySliceBits;
shared uint  numSlices;

shared uint  meshletsId[2*1024];
shared uint  numMeshlets;

shared uint  meshListPtr;

uvec2 pullMeshList() {
  const uint visCount = pos.data[0];
  return uvec2(1, visCount);
  }

// ray related
vec3 rayOrigin(const vec4 wpos) {
  vec4 shPos = scene.viewVirtualShadowLwc * wpos;
  shPos.xyz /= shPos.w;
  return shPos.xyz;
  }

vec4 worldPos(ivec2 frag, float depth) {
  const vec2 fragCoord = ((frag.xy+0.5)*scene.screenResInv)*2.0 - vec2(1.0);
  const vec4 scr       = vec4(fragCoord.x, fragCoord.y, depth, 1.0);
  return scene.viewProjectLwcInv * scr;
  //return scene.viewProjectInv * scr;
  }

//
uint depthSlice(const float z) {
  float dZ   = linearDepth(z,      scene.clipInfo);
  float d0   = linearDepth(0,      scene.clipInfo);
  float d1   = linearDepth(0.9999, scene.clipInfo);
  float d    = (dZ-d0)/(d1-d0);

  return min(uint(d*MaxSlices+0.5), MaxSlices);
  }

void processFragment(const ivec2 fragCoord) {
  const float lamb   = texelFetch(outputImage, fragCoord, 0).x;
  const bool  actRay = !(lamb==0);
  if(!actRay)
    return;

  const float z = texelFetch(depth, fragCoord, 0).x;
  if(z==1.0)
    return;

  const uint  slice = depthSlice(z);
  const vec4  wpos  = worldPos(fragCoord.xy, z);
  const vec3  ray   = rayOrigin(wpos);

  atomicOr(raySliceBits, 1u << slice);
  atomicMin(rayTileBbox[slice].x,   floatToOrderedUint(ray.x));
  atomicMin(rayTileBbox[slice].y,   floatToOrderedUint(ray.y));
  atomicMax(rayTileBbox[slice].z,   floatToOrderedUint(ray.x));
  atomicMax(rayTileBbox[slice].w,   floatToOrderedUint(ray.y));
  atomicMin(rayTileMinDepth[slice], floatToOrderedUint(ray.z));
  }

void tileBbox() {
  const uint  laneID = gl_LocalInvocationIndex;
  const uvec2 srcSz  = textureSize(depth,0);
  const ivec2 tileSz = ivec2(RTSM_LARGE_TILE);

  const ivec2 at0 = ivec2(gl_WorkGroupID.xy) * tileSz;
  const ivec2 xy0 = ivec2(gl_LocalInvocationID.xy);
  for(int x=xy0.x; x<tileSz.x; x+=int(gl_WorkGroupSize.x)) {
    for(int y=xy0.y; y<tileSz.y; y+=int(gl_WorkGroupSize.y)) {
      ivec2 at = at0 + ivec2(x,y);
      if(any(greaterThanEqual(at,srcSz)))
        continue;
      processFragment(at);
      }
    }
  barrier();

  vec4 rbb; float rayD;
  if(laneID<rayTileBbox.length()) {
    rbb.x = orderedUintToFloat(rayTileBbox[laneID].x);
    rbb.y = orderedUintToFloat(rayTileBbox[laneID].y);
    rbb.z = orderedUintToFloat(rayTileBbox[laneID].z);
    rbb.w = orderedUintToFloat(rayTileBbox[laneID].w);
    rayD  = orderedUintToFloat(rayTileMinDepth[laneID]);
    }
  barrier();

  if(laneID<rayTileBbox.length() && (raySliceBits & (1u << laneID))!=0) {
    const uint i = bitCount(raySliceBits & ((1u << laneID)-1u));

    rayTileBbox[i].x   = floatBitsToUint(rbb.x);
    rayTileBbox[i].y   = floatBitsToUint(rbb.y);
    rayTileBbox[i].z   = floatBitsToUint(rbb.z);
    rayTileBbox[i].w   = floatBitsToUint(rbb.w);
    rayTileMinDepth[i] = floatBitsToUint(rayD);
    }
  numSlices = bitCount(raySliceBits);
  }

// meshlets
bool isAabbVisible(const vec4 aabb, const float depthMax) {
  for(uint i=0; i<numSlices; ++i) {
    vec4 rbb; float rayd;
    rbb.x = uintBitsToFloat(rayTileBbox[i].x);
    rbb.y = uintBitsToFloat(rayTileBbox[i].y);
    rbb.z = uintBitsToFloat(rayTileBbox[i].z);
    rbb.w = uintBitsToFloat(rayTileBbox[i].w);
    rayd  = uintBitsToFloat(rayTileMinDepth[i]);
    if(rayd > depthMax)
      continue;
    if(!bboxIntersect(aabb, rbb))
      continue;
    return true;
    }
  return false;
  }

bool isMeshletVisible(uint ptr) {
  vec4  aabb     = pullMeshAabb(ptr);
  float depthMax = pullMeshDepthMax(ptr);
  return isAabbVisible(aabb, depthMax);
  }

void traceMeshlets(const uint tilePtr, const uint visCount) {
  const uint laneID = gl_LocalInvocationIndex;

  for(uint i=laneID; i<visCount; i+=NumThreads) {
    const uint ptr = pos.data[tilePtr + i];

    if(isMeshletVisible(ptr)) {
      uint id = atomicAdd(numMeshlets,1);
      if(id<meshletsId.length())
        meshletsId[id] = ptr;
      }
    }
  }

void main() {
  const uint laneID   = gl_LocalInvocationIndex;

  raySliceBits = 0;
  if(laneID<rayTileBbox.length()) {
    rayTileBbox[laneID]     = uvec4(0xFFFFFFFF, 0xFFFFFFFF, 0, 0);
    rayTileMinDepth[laneID] = 0xFFFFFFFF;
    }

  numMeshlets  = 0;
  barrier();

  tileBbox();
  barrier();

  const uvec2 meshListSrc = pullMeshList();
  traceMeshlets(meshListSrc.x, meshListSrc.y);
  barrier();

  if(numMeshlets>meshletsId.length()) {
    // too many meshlets - pass original mesh-list futher
    if(gl_LocalInvocationIndex==0)
      imageStore(largeTiles, ivec2(gl_WorkGroupID.xy), uvec4(meshListSrc,0,0));
    return;
    }

  if(gl_LocalInvocationIndex==0) {
    meshListPtr = numMeshlets==0 ? NULL : atomicAdd(pos.alloc, numMeshlets);
    if(meshListPtr+numMeshlets>pos.data.length()) {
      // out of memory
      meshListPtr = NULL;
      numMeshlets = 0;
      }
    }
  barrier();

  // store pointer to meshlets list and list-data
  if(gl_LocalInvocationIndex==0)
    imageStore(largeTiles, ivec2(gl_WorkGroupID.xy), uvec4(meshListPtr, numMeshlets, 0, 0));
  for(uint i=laneID; i<numMeshlets; i+=NumThreads)
    pos.data[meshListPtr + i] = meshletsId[i];
  }
